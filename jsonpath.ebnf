(*
 * 金棒 (kanabō)
 * Copyright (c) 2012 Kevin Birch <kmb@pobox.com>.  All rights reserved.
 *
 * 金棒 is a tool to bludgeon YAML and JSON files from the shell: the strong
 * made stronger.
 *
 * For more information, consult the README file in the project root.
 *
 * Distributed under an [MIT-style][license] license.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of
 * this software and associated documentation files (the "Software"), to deal with
 * the Software without restriction, including without limitation the rights to
 * use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of
 * the Software, and to permit persons to whom the Software is furnished to do so,
 * subject to the following conditions:
 *
 * - Redistributions of source code must retain the above copyright notice, this
 *   list of conditions and the following disclaimers.
 * - Redistributions in binary form must reproduce the above copyright notice, this
 *   list of conditions and the following disclaimers in the documentation and/or
 *   other materials provided with the distribution.
 * - Neither the names of the copyright holders, nor the names of the authors, nor
 *   the names of other contributors may be used to endorse or promote products
 *   derived from this Software without specific prior written permission.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
 * FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE CONTRIBUTORS
 * OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
 * WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
 * CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS WITH THE SOFTWARE.
 *
 * [license]: http://www.opensource.org/licenses/ncsa
 *)

path
    = absolute path
    | relative path
    ;

absolute path
    = "$", [ qualified path ]
    ;

qualified path
    = recursive location
    | relative location
    ;

recurisive location
    = "..", relative path
    ;

relative location
    = ".", relative path
    ;

relative path
    = step, [ qualified path ]
    ;

step
    = node test, [ predicate ]
    ;

node test
    = name test
    | node type, "(", ")"
    ;

name test
    = "*"
    | name
    ;

node type
    = "object"
    | "array"
    | "string"
    | "number"
    | "boolean"
    | "null"
    ;

name
    = "'", name character, { name character }, "'"
    = name character, { name character }
    ;

name character
    = ? any unicode character except '.' and '[' ?
    ;

predicate
    = "[", predicate expression , "]"
    ;

predicate expression
    = wildcard
    | subscript
    | slice
    | union
    | filter
    | script
    ;

wildcard
    = "*"
    ;

subscript
     = integer
     ;

slice
    = ":", signed integer, [ ":", integer ]
    | signed integer, ":", [ ":", integer ]
    | signed integer, ":", signed integer, [ ":", integer ]
    ;

union
    = integer, ",", integer
    | union expression, ",", union expression
    ;

union expression
    = relative path
    | filter expression
    ;

filter
    = "?(", filter expression, ")"
    ;

filter expression
    = or expr
    ;

or expr
    = and expr, [ "or", or expr ]
    ;

and expr
    = equality expr, [ "and", and expr ]
    ;

equality expr
    = relational expr, [ equality op, equality expr ]
    ;

equality op
    = "="
    | "!="
    ;

relational expr
    = additive expr, [ relational op, relational expr ]
    ;

relational op
    = ">"
    | "<"
    | ">="
    | "<="
    ;

additive expr
    = multiplicative expr, [ additive op, additive expr ]
    ;

additive op
    = "+"
    | "-"
    ;

multiplictive expr
    = unary expr, [ multiplicative op, multiplicative expr ]
    ;

multiplicative op
    = "*"
    | "/"
    | "%"
    ;

unary expr
    = "@", qualified path
    | number
    | string
    | boolean
    | "null"
    ;

number
    = signed integer, [ fractional part ], [ exponent ]
    ;

fractional part
    = ".", digit, { digit }
    ;

exponent
    = e, digit, { digit }
    ;

e
    = "e" | "E", [ "+" | "-" ]
    ;

string
    = """, { character | escape }, """
    ;

character
    = ? any unicode character except " or \ or control characters ?
    ;

escape
    = "\""
    | "\\"
    | "\/"
    | "\b"
    | "\f"
    | "\n"
    | "\r"
    | "\t"
    | "\u", 4 * hex digit
    ;

hex digit
    = 2 * ( digit |  hex lower | hex upper )
    ;

boolean
    = "true"
    | "false"
    ;

integer
    = "0" | digit one to nine, { digit }
    ;

signed integer
    = [ "-" ], integer
    ;

digit one to nine
    = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
    ;

digit
    = "0" | digit one to nine
    ;

hex lower
    = "a" | "b" | "c" | "d" | "e" | "f"
    ;

hex upper
    = "A" | "B" | "C" | "D" | "E" | "F"
    ;

script
    = "(", script expression, ")"
    ;

script expression
    = ? implementation defined, ouside the scope of this description ?
    ;

