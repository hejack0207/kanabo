
path
    = absolute path
    | relative path
    ;

absolute path
    = "$", [ qualified path ]
    ;

qualified path
    = ".", relative path
    | abbreviated relative path
    ;

relative path
    = step, { qualified path }
    ;

abbreviated relative path
    = "..", relative path
    ;

step
    = node test, { predicate }
    ;

(* 
   refactor this:
   node type tests should be terminal,  and not support predicates
   only name tests have predicates and can continue
 *)
node test
    = name test
    | node type test, "()"
    ;

name test
    = "*"
    | name
    ;

node type test
    = "object"
    | "array"
    | "string"
    | "number"
    | "boolean"
    | "null"
    ;

(* this production may be quoted with single quotes, which will be discarded *)
name
    = ? any sequence of unicode characters and/or escape sequences, except '.' and '[' ?
    ;

predicate
    = wildcard
    | subscript
    | slice
    | union
    | filter
    | script
    ;

wildcard
    = "[", "*", "]"
    ;

subscript
     = "[", integer, "]"
     ;

slice
    = "[" [ integer ], ":", integer, [ ":", integer ], "]"
    ;

union
    = "[", path expression, ",", path expression, "]"
    ;

path expression
    = path
    (* xxx - more needs to be added here, eg @path expressions *)
    ;

filter
    = "[", "?(", expression, ")", "]"
    ;

expression
    = ? TBD ?
    ;

script
    = "[", "(", script expression, ")", "]"
    ;

script expression
    = ? TBD ?
    ;

integer
    = digit, { digit }
    ;

digit
    = 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
    ;


